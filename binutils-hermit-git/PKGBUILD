_pkgname="binutils-hermit"
pkgname=$_pkgname-git
pkgver=r94430.c2f0be9fd4
pkgrel=1
pkgdesc="Cross-build binary utilities for HermitCore"
arch=('x86_64')
url="https://github.com/hermitcore/binutils"
license=('GPL')
groups=()
depends=()
makedepends=('git')
options=('libtool' 'staticlibs' '!buildflags' '!strip')
provides=('binutils-hermit')
conflicts=('binutils-hermit')
source=("$_pkgname::git+https://github.com/hermitcore/binutils.git#branch=hermit")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgname"

	# hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
	sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
	cd "$srcdir/$_pkgname"

	rm -rf build
	mkdir build
	cd build
	../configure --target=$CARCH-hermit --prefix=/opt/hermit --disable-shared --disable-nls --disable-gdb --disable-libdecnumber --disable-readline --disable-sim --disable-libssp --enable-tls --disable-multilib
	make
}

package() {
	cd "$srcdir/$_pkgname/build"
	make DESTDIR="$pkgdir/" install
	rm "$pkgdir/opt/hermit/share/info/dir"
}
