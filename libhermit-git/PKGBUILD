_pkgname="libhermit"
pkgname=$_pkgname-git
pkgver=r1728.01c38a4bc
pkgrel=1
pkgdesc="Cross-build binary utilities for HermitCore"
arch=('x86_64')
url="https://github.com/hermitcore/libhermit"
license=('GPL')
groups=()
depends=()
makedepends=('git' 'cmake' 'binutils-hermit' 'gcc-hermit-bootstrap' 'newlib-hermit' 'pte-hermit')
options=('libtool' 'staticlibs' '!buildflags' '!strip')
provides=('libhermit')
conflicts=('libhermit')
source=("$_pkgname::git+https://github.com/hermitcore/libhermit.git#branch=master"
		"git+https://github.com/hermitcore/LwIP.git")
md5sums=('SKIP'
		 'SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgname"
	git submodule init
	git config submodule.lwip.url $srcdir/LwIP
	git submodule update
}

build() {
	export PATH=/opt/hermit/bin:$PATH

	cd "$srcdir/$_pkgname"

	rm -rf build
	mkdir build
	cd build
	cmake -DTOOLCHAIN_BIN_DIR=/opt/hermit/bin -DCMAKE_INSTALL_PREFIX=/opt/hermit -DBOOTSTRAP=true ..
	make hermit-bootstrap
}

package() {
	cd "$srcdir/$_pkgname/build"
	make DESTDIR="$pkgdir/" hermit-bootstrap-install
}
