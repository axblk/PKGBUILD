_pkgname="gcc-hermit-bootstrap"
pkgname=$_pkgname-git
pkgver=r146655.50eced1900b
_islver=0.18
pkgrel=1
pkgdesc="Cross-build binary utilities for HermitCore"
arch=('x86_64')
url="https://github.com/hermitcore/gcc"
license=('GPL')
groups=()
depends=()
makedepends=('git')
options=('libtool' 'staticlibs' '!buildflags' '!strip')
provides=('gcc-hermit-bootstrap')
conflicts=('gcc-hermit-bootstrap')
source=("$_pkgname::git+https://github.com/hermitcore/gcc.git#branch=bootstrap"
		"http://isl.gforge.inria.fr/isl-${_islver}.tar.bz2")
md5sums=('SKIP'
		 '11436d6b205e516635b666090b94ab32')

pkgver() {
	cd "$srcdir/$_pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgname"

	# link isl for in-tree build
	rm -rf isl
	ln -s ../isl-${_islver} isl

	sed -i 's/STMP_FIXINC=stmp-fixinc/STMP_FIXINC=/g' gcc/configure

	# hack! - some configure tests for header files using "$CPP $CPPFLAGS"
	sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
}

build() {
	cd "$srcdir/$_pkgname"

	rm -rf build
	mkdir build
	cd build
	../configure --target=$CARCH-hermit --prefix=/opt/hermit --without-headers --with-isl --disable-multilib --without-libatomic --with-tune=generic --enable-languages=c,c++,lto --disable-nls --disable-shared --disable-libssp --enable-threads=posix --disable-libgomp --enable-tls --enable-lto --disable-symvers
	make all-gcc
}

package() {
	cd "$srcdir/$_pkgname/build"
	make DESTDIR="$pkgdir/" install-gcc
	rm "$pkgdir/opt/hermit/share/info/dir"
}
