_pkgname="newlib-hermit"
pkgname=$_pkgname-git
pkgver=r17151.7db17585c
pkgrel=1
pkgdesc="Cross-build binary utilities for HermitCore"
arch=('x86_64')
url="https://github.com/hermitcore/newlib"
license=('GPL')
groups=()
depends=()
makedepends=('git' 'binutils-hermit' 'gcc-hermit-bootstrap')
options=('libtool' 'staticlibs' '!buildflags' '!strip')
provides=('newlib-hermit')
conflicts=('newlib-hermit')
source=("$_pkgname::git+https://github.com/hermitcore/newlib.git#branch=hermit")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	export PATH=/opt/hermit/bin:$PATH

	export CROSSCOMPREFIX=x86_64-hermit
	export STACKPROT=-fno-stack-protector
	export CFLAGS_FOR_TARGET="-m64 -mtls-direct-seg-refs -O3 -ftree-vectorize"
	export FCFLAGS_FOR_TARGET="-m64 -mtls-direct-seg-refs -O3 -ftree-vectorize"
	export FFLAGS_FOR_TARGET="-m64 -mtls-direct-seg-refs -O3 -ftree-vectorize"
	export CXXFLAGS_FOR_TARGET="-m64 -mtls-direct-seg-refs -O3 -ftree-vectorize"

	cd "$srcdir/$_pkgname"

	rm -rf build
	mkdir build
	cd build
	../configure --target=$CARCH-hermit --disable-shared --disable-multilib --disable-multilib --enable-lto --enable-newlib-hw-fp --enable-newlib-io-c99-formats --enable-newlib-multithread --disable-multilib --prefix=/opt/hermit #--enable-newlib-reent-small
	make
}

package() {
	cd "$srcdir/$_pkgname/build"
	make DESTDIR="$pkgdir/" install
}
