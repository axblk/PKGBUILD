_pkgname="gcc-hermit"
pkgname=$_pkgname-git
pkgver=r146650.ab099d21c4f
pkgrel=1
pkgdesc="Cross-build binary utilities for HermitCore"
arch=('x86_64')
url="https://github.com/hermitcore/gcc"
license=('GPL')
groups=()
depends=()
makedepends=('git' 'gcc-hermit-bootstrap' 'newlib-hermit' 'pte-hermit' 'libhermit')
options=('libtool' 'staticlibs' '!buildflags' '!strip')
provides=('gcc-hermit')
conflicts=('gcc-hermit')
source=("$_pkgname::git+https://github.com/hermitcore/gcc.git#branch=hermit")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgname"

	sed -i 's/STMP_FIXINC=stmp-fixinc/STMP_FIXINC=/g' gcc/configure

	# hack! - some configure tests for header files using "$CPP $CPPFLAGS"
	sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
}

build() {
	export PATH=/opt/hermit/bin:$PATH

	cd "$srcdir/$_pkgname"

	rm -rf build
	mkdir build
	cd build
	../configure --target=$CARCH-hermit --prefix=/opt/hermit --with-newlib --disable-multilib --without-libatomic --with-tune=generic --enable-languages=c,c++,go,fortran,lto --disable-nls --disable-shared --disable-libssp --enable-threads=posix --disable-libgomp --enable-tls --enable-lto --disable-symvers
	make
}

package() {
	cd "$srcdir/$_pkgname/build"
	make DESTDIR="$pkgdir/" install
	rm "$pkgdir/opt/hermit/share/info/dir"
}
